/* 
 Gardino 1.0
 6/28/2014
 By Jeffrey Dorfman
 
 The goal of this project is to maintain proper soil moisture for an outdoor plant. It does so by pumping water into the soil
 once a day, and relying on a moisture sensor to test if the soil is already wet (i.e. from rainfall). It will also add an extra
 afternoon watering (heat of the day) if the soil moisture falls below acceptable levels. The ability to water now, or skip 
 next watering, is available via push button on the side of the arduino enclosure.
 As it currently stands, the project maintains two plants, but only has one moisture sensor. They are watered at the 
 same time, and the single moisture sensor overrides the pump in event of rain.
 Eventually, I would like this project to involve several potted plants. The ideal being is that they require little maintenance,
 other than refilling the reservoir. I would also like to add the option to mist leaves, possibly in cases of extreme heat causing
 leaf wilt.
 
 
 HARDWARE:
 
 * Arduino Uno R3
 * Adafruit Motor Shield v1.2
 * Adafruit DS1307 Breakout
 * Pushbutton w/100 resistor
 * 12v power source
 * Elecrow Moisture Sensor (5v)
 * 12v Micro water pump (~1 liter/18seconds)
 * LED with Resistor on pin 2
 * Harbor Freight Drip Irrigation Kit http://www.harborfreight.com/irrigation-drip-kit-46095.html
 * 2 Gallon Reservoir
 
 
 LIBRARIES UTILIZED:
 
 * Adafruit Motor shield library - copyright Adafruit Industries LLC, 200910
 * @WizenenEE's runEvery() Macro - http://forum.arduino.cc/index.php?topic=124974.0
 
 
 MOISTURE SENSOR:
 
 Value range:
 0 ~300 : dry soil
 300~700 : humid soil
 700~950 : in water
 
 
 */

// Libraries
#include <AFMotor.h>
#include <Wire.h>
#include "RTClib.h"


// runEvery macro
#define runEvery(t) for (static uint16_t _lasttime;\
                        (uint16_t)((uint16_t)millis() - _lasttime) >= (t);\
                         _lasttime += (t))


// ---- CONSTANTS ----
RTC_DS1307 rtc;    // Clock Module
AF_DCMotor motor(2, MOTOR12_1KHZ);   // Motor
const int led_A_Pin = 2;
const int buttonPin = 10; // the pin number for the button
const int buttonInterval = 500; // number of millisecs between button readings
const int runNowLimit = 10;  //Time button must be held to override pump 2 = 1 second
const int sensorPin = 15;    //Moisture Sensor
const int blinkLimit = 5;    //Total number of Led Blinks
const int blinkDurationFast = 100; // number of millisecs for Led ON
const int blinkDurationSlow = 250; // number of millisecs for Led ON
long liter = 300000L;    // Water Calibration - adjust this time value until water amount is correct - in Millis


// ----- Define State Machine -----
typedef enum {  
  ON,
  OFF}
states;
states state = OFF;

//------- VARIABLES --------
byte waterNow = LOW;
byte lastButtonState = LOW;
byte skipNext = LOW;    //will skip next watering if HIGH
byte led_A_State = LOW;           //   LOW = off
byte runOncePump = LOW;
byte runFastLed = HIGH;
byte runSlowLed = LOW;
byte ledRunning = LOW;
byte buttonHeld = LOW;
int buttonState;  
int waterOverride = 0;    // if button is held, the system will waterNow, then reset skipnext
int sensorValue;    //define moisture readout
int blinkCountF = 0;    //counts # of time led has gone HIGH + LOW
int blinkCountS = 0;    //counts # of time led has gone HIGH + LOW
int wetThreshold = 670;
int dryThreshold = 400;
unsigned long currentMillis = 0;    // stores the value of millis() in each iteration of loop()
unsigned long led_A_Millis = 0;
unsigned long pumpTimeMillis;


// ------------- ALARMS ------------- 
const int alarmMorningHour = 4;    // set alarm hour (All in 24 hour Time, NOT 12 hour)
const int alarmMorningMinute = 30;  // set alarm minute
const int alarmEveHour = 16;    // set alarm hour 
const int alarmEveMinute = 30;  // set alarm minute


//=================================================================

void setup() {

  // set up Serial library at 9600 bps if detected
  if (Serial) {
    Serial.begin(57600);
    Serial.println("Gardino.ino Test");
    Serial.println();
    Serial.println("Debug Mode ACTIVE");
    Serial.println();
  }

  //Power for DS1307 and Initializes
  pinMode (17, OUTPUT);    
  digitalWrite (17, HIGH);
  pinMode (16, OUTPUT);
  digitalWrite (16, LOW);
  Wire.begin();
  rtc.begin();
  rtc.adjust(DateTime(__DATE__, __TIME__));
  // ******Use Above code to set the rtc if needed*****

  //debug check for RTC
  if (! rtc.isrunning()) { 
    Serial.println("RTC is NOT running!");
  }

  //Defines motor speed
  motor.setSpeed(255);
  motor.run(RELEASE);

  //sets up indicator LED
  pinMode (led_A_Pin, OUTPUT);
  digitalWrite (led_A_Pin, LOW);

  //button
  pinMode(buttonPin, INPUT);

  //debug check for RTC
  while (! rtc.isrunning()) { 
    Serial.println("RTC is NOT running!");
    runFastLed = HIGH;
  }
}

//============================================

void loop() {

  currentMillis = millis();
  readButton();
  checkMoisture();
  checkWaterAlarm();
  runPump();
  eStop();
  ledSlow();
  ledFast();
  debug();

}

//============================================

void checkMoisture() {

  runEvery (3000){
    sensorValue = analogRead(sensorPin);

    if (skipNext == LOW && sensorValue >= wetThreshold) {
      skipNext = !skipNext;
      ledFast = HIGH;
      Serial.println("Skipping next Watering STORED");
    }
    if (skipNext == HIGH && sensorValue <= dryThreshold) {
      skipNext = !skipNext;
      ledFast = HIGH;
      Serial.println("Next Watering Reinstated");
    }
  }

}

//============================================

void checkWaterAlarm () {
  runEvery (1000){
    DateTime now = rtc.now();

    if (now.hour() == alarmMorningHour && now.minute() == alarmMorningMinute && now.second() == 0){

      if (skipNext == HIGH) {
        Serial.println("Watering Has Been Skipped");
        runSlowLed = HIGH;
        skipNext = !skipNext;
      }

      else {
        waterNow = HIGH;
      }
    }
    if (now.hour() == alarmEveHour && now.minute() == alarmEveMinute && now.second() == 0){

      if (skipNext == HIGH) {
        Serial.println("Watering Has Been Skipped");
        runSlowLed = HIGH;
        skipNext = !skipNext;
      }

      else {
        waterNow = HIGH;
      }
    }
  }
}

//============================================

void readButton() {

  runEvery (buttonInterval){
    int buttonState = digitalRead(buttonPin);

    if (buttonState == HIGH) {

      if (skipNext == HIGH) {
        if (waterOverride >= runNowLimit) {
          waterNow = HIGH;
          skipNext = !skipNext;
          buttonHeld = HIGH;
          Serial.println("Pump Overridden - RUNNING NOW - Next Watering Reinstated");
        }
        Serial.println("Next Watering Already Stored");
        runFastLed = HIGH;
        waterOverride++;
      }

      else {
        skipNext = !skipNext;
        Serial.println("Skipping next Watering STORED");
        runSlowLed = HIGH;
      }
    }
    else {
      buttonHeld = LOW;
    }
  }
}

//============================================

void displayTime() {

  runEvery (1000){
    DateTime now = rtc.now();  

    if (now.hour() >= 13) {
      Serial.print(now.hour() - 12, DEC);
    }
    else {
      Serial.print(now.hour(), DEC);
    }
    Serial.print(':');
    Serial.print(now.minute(), DEC);
    Serial.print(':');
    Serial.print(now.second(), DEC);
    if (now.hour() >= 13) {
      Serial.print(" PM");
    }
    else {
      Serial.print(" AM");
    }
    Serial.println();
  }
}

//============================================

void ledFast() {

  if (runFastLed == HIGH && ledRunning == LOW && blinkCountF == 0) {
    led_A_Millis = (currentMillis + blinkDurationFast);
    blinkCountF++;
    ledRunning = !ledRunning;
    runFastLed = !runFastLed;
  }

  if (blinkCountF >= 1) {
    if (led_A_State == LOW) {
      if (currentMillis >= led_A_Millis) {
        led_A_Millis = (currentMillis + blinkDurationFast);
        led_A_State = HIGH;
      }
    }

    else {
      if (currentMillis >= led_A_Millis) {
        led_A_Millis = (currentMillis + blinkDurationFast);
        led_A_State = LOW;
        blinkCountF++;
      }
    }

    if (blinkCountF >= blinkLimit) {
      blinkCountF = 0;
      ledRunning = !ledRunning;
    }

    digitalWrite (led_A_Pin, led_A_State);
  }

}

//============================================

void ledSlow() { 
 
  if (runSlowLed == HIGH && ledRunning == LOW && blinkCountS == 0) {
    led_A_Millis = (currentMillis + blinkDurationSlow);
    blinkCountS++;
    ledRunning = !ledRunning;
    runSlowLed = !runSlowLed;
  }

  if (blinkCountS >= 1) {
    if (led_A_State == LOW) {
      if (currentMillis >= led_A_Millis) {
        led_A_Millis = (currentMillis + blinkDurationSlow);
        led_A_State = HIGH;
      }
    }

    else {
      if (currentMillis >= led_A_Millis) {
        led_A_Millis = (currentMillis + blinkDurationSlow);
        led_A_State = LOW;
        blinkCountS++;
      }
    }

    if (blinkCountS >= blinkLimit) {
      blinkCountS = 0;
      ledRunning = !ledRunning;
    }

    digitalWrite (led_A_Pin, led_A_State);
  }

}

//============================================

void runPump() {

  if(waterNow == HIGH) {
    if (runOncePump == LOW) {
      state = ON;
      Serial.println("Pump ON");
      Serial.println();
      pumpTimeMillis = (currentMillis + liter);
      runOncePump = !runOncePump;
      waterOverride = 0;
      waterNow = !waterNow;
    }
  }

  if (currentMillis <= pumpTimeMillis){
    state = ON;
  }

  else {
    if (runOncePump == HIGH) {
      Serial.println("Pump OFF");
      Serial.println();
      runOncePump = !runOncePump;
    }
    state = OFF;
  }

  switch (state) {

  case ON:
    motor.run(FORWARD);
    digitalWrite(led_A_Pin, HIGH);
    break;

  case OFF:
    motor.run(RELEASE);
    digitalWrite(led_A_Pin, LOW);
    break;

  }
}

//============================================

void eStop() {

  runEvery(250){
    if (state == ON) {
      int buttonState = digitalRead(buttonPin);
      if (buttonState == HIGH && buttonHeld == LOW) {
        pumpTimeMillis = currentMillis;
        motor.run(RELEASE);
        digitalWrite(led_A_Pin, LOW);
        state = OFF;
        Serial.println("----------===== EMERGENCY STOP =====-----------");
        delay(500);
      }
    }
  }
}


//============================================

void debug() {
  displayTime();
  runEvery(20000){
    DateTime now = rtc.now();
    sensorValue = analogRead(sensorPin);
    int sec = now.second();
    Serial.println();
    Serial.println();
    Serial.print("Morning Alarm Set for ");
    if (alarmMorningHour >= 13) {
      Serial.print(alarmMorningHour - 12, DEC);
    }
    else {
      Serial.print(alarmMorningHour, DEC);
    }
    Serial.print(':');
    Serial.print(alarmMorningMinute, DEC);
    Serial.print(':');
    Serial.print("00");
    if (alarmMorningHour >= 13) {
      Serial.print(" PM");
    }
    else {
      Serial.print(" AM");
    }
    Serial.println();
    Serial.print("Evening Alarm Set for ");
    if (alarmEveHour >= 13) {
      Serial.print(alarmEveHour - 12, DEC);
    }
    else {
      Serial.print(alarmEveHour, DEC);
    }
    Serial.print(':');
    Serial.print(alarmEveMinute, DEC);
    Serial.print(':');
    Serial.print("00");
    if (alarmEveHour >= 13) {
      Serial.print(" PM");
    }
    else {
      Serial.print(" AM");
    }
    Serial.println();
    Serial.print("WaterOverride = ");
    Serial.println(waterOverride);  //if >=5 should flip bit WaterNow
    Serial.print("WaterNow = ");
    Serial.println(waterNow);
    Serial.print("Pump = ");
    Serial.println(state);
    Serial.print("Current Pump Run Time = ");
    Serial.print(liter / 1000);
    Serial.println(" seconds");
    if (sensorValue == 0) {
      Serial.println ("Moisture Sensor NOT IN SOIL");
    }
    else if (sensorValue <= 300) {
      Serial.println ("Soil is Dry");
    }
    else if (sensorValue > 300 && sensorValue <= 700) {
      Serial.println ("Soil is Humid");
    }
    else if (sensorValue > 700) {
      Serial.println ("Soil is Soaking");
    }
    Serial.print("Actual MoistureSensor = " );                       
    Serial.println(sensorValue);
    Serial.print("skipNext = ");
    Serial.println(skipNext);
    Serial.println();
    Serial.println();
  }
}

//===============END================
